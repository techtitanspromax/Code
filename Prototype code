#include "thingProperties.h"
#include <Wire.h>
#include <LCD_I2C.h>
#define relayPin 2
#define wet 210
#define dry 510

int analogInPinA0 = A0;  // Soil moisture sensor pin
int analogInPin = A2;    // Battery voltage analog pin
int sensorValue;
float calibration = 0.36; // Adjust this using multimeter reading

LCD_I2C lcd(0x27, 20, 4);

unsigned long lastUpdate = 0;
const unsigned long updateInterval = 1000; // 1s

void setup() {
  Serial.begin(9600);
  delay(1500);

  pinMode(relayPin, OUTPUT);
  digitalWrite(relayPin, HIGH); // Relay off

  initProperties();
  ArduinoCloud.begin(ArduinoIoTPreferredConnection);
  setDebugMessageLevel(2);
  ArduinoCloud.printDebugInfo();

  Wire.begin();
  lcd.begin();
  lcd.backlight();
  lcd.clear();

}

void loop() {
  ArduinoCloud.update();

  unsigned long currentMillis = millis();
  if (currentMillis - lastUpdate >= updateInterval) {
    lastUpdate = currentMillis;

    readSoilMoisture();
    readBatteryVoltage();
    updateLCD();
  }
}

void readBatteryVoltage() {
  int batteryRaw = analogRead(analogInPin); // Read A2
  voltage = ((batteryRaw * 5.0) / 1023.0) * 2 + calibration; // Voltage divider + calibration
  bat_percentage = mapfloat(voltage, 2.8, 7.4, 0, 100);

  // Clamp percentage between 1 and 100
  if (bat_percentage >= 100) bat_percentage = 100;
  if (bat_percentage <= 0) bat_percentage = 1;

  // Debug output
  Serial.print("Battery Raw = ");
  Serial.print(batteryRaw);
  Serial.print("\tVoltage = ");
  Serial.print(voltage);
  Serial.print(" V\tBattery % = ");
  Serial.println(bat_percentage);
}

float mapfloat(float x, float in_min, float in_max, float out_min, float out_max) {
  return (x - in_min) * (out_max - out_min) / (in_max - in_min) + out_min;
}

void readSoilMoisture() {
  int rawValue = analogRead(analogInPinA0);
  int mappedMoisture = map(rawValue, dry, wet, 0, 100);
  sensor = constrain(mappedMoisture, 0, 100);
}



void onRelayChange() {
  if (relay) {
    digitalWrite(relayPin, LOW); // Turn ON
  } else {
    digitalWrite(relayPin, HIGH); // Turn OFF
  }
}

void updateLCD() {
  lcd.setCursor(0, 0);
  lcd.print("Moisture: ");
  lcd.print(sensor);
  lcd.print("%   ");

  lcd.setCursor(0, 1);
  if (sensor >= 70) {
    lcd.print("Soil: Wet     ");
  } else if (sensor >= 40) {
    lcd.print("Soil: Moist   ");
  } else {
    lcd.print("Soil: Dry     ");
  }
  lcd.setCursor(0, 3);
  lcd.print("Battery: ");
  lcd.print(bat_percentage);
  lcd.print("%   ");
}

void onSensorChange() {}
void onLcdChange() {}
